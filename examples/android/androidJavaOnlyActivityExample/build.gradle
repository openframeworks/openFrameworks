apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 17
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    // Configure source folders like Eclipse
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            jni.srcDirs = ['jni']
            java.srcDirs = ['srcJava']
            resources.srcDirs = ['srcJava']
            aidl.srcDirs = ['srcJava']
            renderscript.srcDirs = ['srcJava']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
    }
}

dependencies {
    compile project(':ofAndroidLib')
}

task compileReleaseOF(dependsOn: rootProject.ofNdkSetup) << {
    rootProject.ofRunMake(["-C", projectDir.absolutePath, "PLATFORM_OS=Android", "Release"])
}

task compileDebugOF(dependsOn: rootProject.ofNdkSetup) << {
    rootProject.ofRunMake(["-C", projectDir.absolutePath, "PLATFORM_OS=Android", "Debug"])
}

task cleanOF(dependsOn: rootProject.ofNdkSetup) << {
    rootProject.ofRunMake(["-C", projectDir.absolutePath, "PLATFORM_OS=Android", "clean"])
}

clean {
    dependsOn cleanOF
}

project.afterEvaluate {
    compileDebugNdk.dependsOn(compileDebugOF)
    compileReleaseNdk.dependsOn(compileReleaseOF)
}
